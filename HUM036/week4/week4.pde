// 11711918 Yechang Wu, add support for images sequence.
// Based on P_4_3_2_01.pde
// 
// Generative Gestaltung, ISBN: 978-3-87439-759-9
// First Edition, Hermann Schmidt, Mainz, 2009
// Hartmut Bohnacker, Benedikt Gross, Julia Laub, Claudius Lazzeroni
// Copyright 2009 Hartmut Bohnacker, Benedikt Gross, Julia Laub, Claudius Lazzeroni
//
// http://www.generative-gestaltung.de
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

/**
 * pixel mapping. each pixel is translated into a new element (letter)
 * 
 * KEYS
 * 1                 : toogle font size mode (dynamic/static)
 * 2                 : toogle font color mode (color/b&w)
 * arrow up/down     : maximal fontsize +/-
 * arrow right/left  : minimal fontsize +/-
 * s                 : save png
 * p                 : save pdf
 */

import processing.pdf.*;
import java.util.Calendar;

boolean savePDF = false;

String inputText = "Ihr naht euch wieder, schwankende Gestalten, Die früh sich einst dem trüben Blick gezeigt. Versuch ich wohl, euch diesmal festzuhalten? Fühl ich mein Herz noch jenem Wahn geneigt? Ihr drängt euch zu! nun gut, so mögt ihr walten, Wie ihr aus Dunst und Nebel um mich steigt; Mein Busen fühlt sich jugendlich erschüttert Vom Zauberhauch, der euren Zug umwittert. Ihr bringt mit euch die Bilder froher Tage, Und manche liebe Schatten steigen auf; Gleich einer alten, halbverklungnen Sage Kommt erste Lieb und Freundschaft mit herauf; Der Schmerz wird neu, es wiederholt die Klage.";
float fontSizeMax = 20;
float fontSizeMin = 10;
float spacing = 12; // line height
float kerning = 0.5; // between letters

boolean fontSizeStatic = false;
boolean blackAndWhite = false;

PFont font;

String genFilename(String path, int num) {
  return String.format("%s/%d.jpg", path, num);
}

class CharPicture {
  float rScale = 0.222, gScale=0.707, bScale = 0.071;
  ArrayList<PImage> imgs = new ArrayList<PImage>();
  int now = 0;

  CharPicture(String filename) {
    File file = dataFile(filename);

    if (file.isDirectory()) {
      while (dataFile(genFilename(filename, now)).isFile()) {
        imgs.add(loadImage(genFilename(filename, now++)));
      }
    } else  if (file.isFile()) {
      imgs.add(loadImage(filename));
    } else System.exit(-1);
    now = 0;
  }


  void update() {
    System.out.println(now);
    float x = 0, y = 10;
    int counter = 0;
    PImage img = imgs.get(now++);
    if (now == imgs.size())
      now = 0;

    while (y < height) {
      // translate position (display) to position (image)
      int imgX = (int) map(x, 0, width, 0, img.width);
      int imgY = (int) map(y, 0, height, 0, img.height);

      // get current color
      color c = img.pixels[imgY*img.width+imgX];
      int greyscale = round(red(c)*rScale + green(c)*gScale + blue(c)*bScale);

      pushMatrix();
      translate(x, y);

      if (fontSizeStatic) {
        textFont(font, fontSizeMax);
        if (blackAndWhite) fill(greyscale);
        else fill(c);
      } else {
        // greyscale to fontsize
        float fontSize = map(greyscale, 0, 255, fontSizeMax, fontSizeMin);
        fontSize = max(fontSize, 1);
        textFont(font, fontSize);
        if (blackAndWhite) fill(0);
        else fill(c);
      } 

      char letter = inputText.charAt(counter);
      text(letter, 0, 0);
      float letterWidth = textWidth(letter) + kerning;
      // for the next letter ... x + letter width
      x = x + letterWidth; // update x-coordinate
      popMatrix();

      // linebreaks
      if (x+letterWidth >= width) {
        x = 0;
        y = y + spacing; // add line height
      }

      counter++;
      if (counter > inputText.length()-1) counter = 0;
    }
  }
  
  int getWidth() {
    if (imgs.size() == 0) {
      return 233;
    }
    return imgs.get(0).width;
  }
  int getHeight() {
    if (imgs.size() == 0) {
      return 233;
    }
    return imgs.get(0).height;
  }
}




CharPicture charPicture;

void settings() {
  charPicture = new CharPicture("video");
  //charPicture = new CharPicture("avatar.jpg");
  size(charPicture.getWidth(), charPicture.getHeight());
}

void setup() {
  smooth(); 

  font = createFont("Times", 10);
}

void draw() {
  if (savePDF) beginRecord(PDF, timestamp()+".pdf");

  background(255);
  textAlign(LEFT);
  //textAlign(LEFT,CENTER); //// also nice!

  charPicture.update();

  if (savePDF) {
    savePDF = false;
    endRecord();
  }
}


void keyReleased() {
  if (key == 's' || key == 'S') saveFrame(timestamp()+"_##.png");
  if (key == 'p' || key == 'P') savePDF = true;
  // change render mode
  if (key == '1') fontSizeStatic = !fontSizeStatic;
  // change color stlye
  if (key == '2') blackAndWhite = !blackAndWhite;
  println("fontSizeMin: "+fontSizeMin+"  fontSizeMax: "+fontSizeMax+"   fontSizeStatic: "+fontSizeStatic+"   blackAndWhite: "+blackAndWhite);
}

void keyPressed() {
  // change fontSizeMax with arrowkeys up/down 
  if (keyCode == UP) fontSizeMax += 2;
  if (keyCode == DOWN) fontSizeMax -= 2; 
  // change fontSizeMin with arrowkeys left/right
  if (keyCode == RIGHT) fontSizeMin += 2;
  if (keyCode == LEFT) fontSizeMin -= 2; 

  //fontSizeMin = max(fontSizeMin, 2);
  //fontSizeMax = max(fontSizeMax, 2);
}

// timestamp
String timestamp() {
  Calendar now = Calendar.getInstance();
  return String.format("%1$ty%1$tm%1$td_%1$tH%1$tM%1$tS", now);
}
